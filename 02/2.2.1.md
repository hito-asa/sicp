### 2.2.1 並びの表現

```scheme
(cons 1 (cons 2 (cons 3 (cons 4 (list)))))
; => (1 2 3 4)

(list 1 2 3 4)
; => (1 2 3 4)

(define 1to4 (list 1 2 3 4))

(car 1to4)
; => 1

(cdr 1to4)
; => (2 3 4)

(car (cdr 1to4))
; => 2
```

#### リスト演算

インデックスを指定して取り出し

```scheme
(define (list-ref items n)
  (if (= n 0)
      (car items)
      (list-ref (cdr items) (- n 1))))

(define squares (list 1 4 9 16 25))

(list-ref squares 3)
; => 16
```

長さ

```scheme
(define (length items)
  (if (null? items)
      0
      (+ 1 (length (cdr items)))))

(length (list 1 2 3 4 5))
; => 5

; 反復的バージョン
(define (length items)
  (let iter ((l items) (n 0))
            (if (null? l)
                n
                (iter (cdr l) (+ n 1)))))

(length (list 1 2 3 4 5))
; => 5
```

結合

```scheme
(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))
```


#### ex 2.18

```scheme
(define (reverse l)
  (let iter ((org l) (dst ()))
            (if (null? org)
                dst
                (iter (cdr org) (cons (car org) dst)))))
```

ついでに複製  
ググッたところ、破壊はset-car! / set-cdr!でできるらしい。  
下記のような浅い複製はダメですね。

```scheme
(define (clone l) l)
(define l1 (list 1 2 3))
(define l2 (clone l1))
(set-car! l1 10)
l1
; => (10 2 3)
l2
; => (10 2 3)
```

reverse 2回

```scheme
(define (reverse l)
  (let iter ((org l) (dst ()))
            (if (null? org)
                dst
                (iter (cdr org) (cons (car org) dst)))))

(define l1 (list 1 2 3))
(define l2 (clone l1))
(set-car! l1 10)
l1
; => (10 2 3)
l2
; => (1 2 3)
```

append同様再帰的にやる

```scheme
(define (clone org)
  (let iter ((src org) (dst ()))
            (if (null? src)
                dst
                (cons (car src) (iter (cdr src) dst)))))

(define l1 (list 1 2 3))
(define l2 (clone l1))
(set-car! l1 10)
l1
; => (10 2 3)
l2
; => (1 2 3)
```

#### ex 2.19

1.2.2の両替の手続き

```scheme
(define (count-change amount)
  (cc amount 5))

(define (cc amount kinds-of-coins)
  (cond ((= amount 0) 1)
        ((or (< amount 0) (= kinds-of-coins 0)) 0)
        (else (+ (cc amount (- kinds-of-coins 1))
                 (cc (- amount (first-denomination kinds-of-coins)) kinds-of-coins)))))

(define (first-denomination kinds-of-coins)
  (cond ((= kinds-of-coins 1) 1)
        ((= kinds-of-coins 2) 5)
        ((= kinds-of-coins 3) 10)
        ((= kinds-of-coins 4) 25)
        ((= kinds-of-coins 5) 50)))

(count-change 100)
; => 292
```

これを下記のような硬貨の種類のリストを受け取って処理するようにする。

```scheme
(define us-coins (list 50 25 10 5 1))
(define uk-coins (list 100 50 20 10 5 2 1 0.5))
; ついでに
(define jp-coins (list 500 100 50 10 5 1))
```

ccは問題に書いてある

```scheme
(cc 100 us-coins)
(define (cc amount coin-values)
  (cond ((= amount 0) 1)
        ((or (< amount 0) (no-more? coin-values)) 0)
        (else (+ (cc amount (except-first-denomination coin-values))
                 (cc (- amount (first-denomination coin-values)) coin-values)))))
```

で、first-denomination / except-first-denomination / no-more? をリストの基本的演算を使って定義しろ、との問題。  
名前で明らかですが以下の通り。

```scheme
(define first-denomination car)
(define except-first-denomination cdr)
(define no-more? null?)
(cc 100 us-coins)
; => 292
```

もともと1.2.2の解が大きい硬貨から順に取り出す方法なのでリストの順序に依存する。  
```scheme
(cc 100 (list 50 25 10 5 1))
; => 292
(cc 100 (list 10 25 50 1 5))
; => 292
```

あれ、変わんない・・・もともと変わらないらしい。  
「硬貨どれか1個取り出して残りを・・・」というプロセスっぽい。

#### ex 2.20

可変長引数が使えるのですね。
```scheme
(define (f x y . z) z)
(f 1 2 3 4 5 6)
; => (3 4 5 6)

(define (g . w) w)
(g)
; =>
(g 1 2 3 4 5 6)
; => (1 2 3 4 5 6)
```

ここは迷わずfilterを定義だ！
```scheme
(define (same-parity head . tail)
  (filter tail (lambda (x) (even? (+ head x)))))

(define (filter src f)
  (reverse (let iter ((remain src) (dst ()))
                     (if (null? remain)
                         dst
                         (iter (cdr remain)
                               (if (f (car remain))
                                   (cons (car remain) dst)
                                   dst))))))

; 再帰的にできずreverseが必要に・・・
(define (reverse l)
  (let iter ((org l) (dst ()))
            (if (null? org)
                dst
                (iter (cdr org) (cons (car org) dst)))))

(same-parity 1 2 3 4 5 6 7 8 9 10)
; => (3 5 7 9)
(same-parity 2 1 2 3 4 5 6)
; => (2 4 6)
```

#### リストの写像

```scheme
(define (scale-list items factor)
  (if (null? items)
      ()
      (cons (* (car items) factor)
            (scale-list (cdr items) factor))))

(scale-list (list 1 2 3 4 5) 10)
; => (10 20 30 40 50)
```

map
```scheme
(define (map proc items)
  (if (null? items)
      ()
      (cons (proc (car items))
            (map proc (cdr items)))))

(map abs (list -10 2.5 -11.6 17))
; => (10 2.5 11.6 17)

(map (lambda (x) (* x x)) (list 1 2 3 4))
; => (2 4 9 16)

(define (scale-list items factor)
  (map (lambda (x) (* x factor)) items))

(scale-list (list 1 2 3 4 5) 10)
; => (10 20 30 40 50)
```

#### ex 2.21

下記を満たすsquare-listの定義を書く
```scheme
(square-list (list 1 2 3 4))
; => (1 4 9 16)
```

```scheme
(define (square-list items)
  (if (null? items)
      ()
      (cons (* (car items) (car items))
            (square-list (cdr items)))))

(square-list (list 1 2 3 4))
; => (1 4 9 16)

(define (square-list items)
  (map (lambda (x) (* x x)) items))

(square-list (list 1 2 3 4))
; => (1 4 9 16)
```

#### ex 2.22

下記square-listが逆順になってしまうのはなぜか？
```scheme
(define (square x) (* x x))
(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter (cdr things)
              (cons (square (car things)) answer))))
  (iter items ()))

(square-list (list 1 2 3 4))
; => (16 9 4 1)
```

append, reverse, filterで見た通り、再帰的プロセスを用いないと逆順になってしまう。
carが先頭を取り、consが先頭に付加するものであるため。

そしてこれも動かない。
```scheme
(define (square x) (* x x))
(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter (cdr things)
              (cons answer (square (car things))))))
  (iter items ()))

(square-list (list 1 2 3 4))
; => ((((() . 1) . 4) . 9) . 16)
```

いまさらだ！  
listはpairの左側に値、右側にpairが連なるデータ構造。逆につなげると異なるデータ構造になる。  
（consがlistを作るもの、というよりconsはpairを作りlistは上記のようなpairのかたち、と捉えたほうが良さそう？）

#### ex 2.23

for-eachの定義
```scheme
(for-each (lambda (x) (newline) (display x))
          (list 57 321 88))
; => 57\n321\n88
```

手続き的になってしまった・・・
```scheme
(define (for-each proc items)
  (proc (car items))
  (if (null? (cdr items))
      #t
      (for-each proc (cdr items))))

(for-each (lambda (x) (newline) (display x))
          (list 57 321 88))
; => \n57\n321\n88

; なぜ先に改行なのか
(for-each (lambda (x) (display x) (newline))
          (list 57 321 88))
; => 57\n321\n88\n
```

