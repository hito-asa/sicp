### 2.2.4 例：図形言語
DrRacketのSICPサポートですすめる。  
http://www.neilvandyke.org/racket-sicp/

#### ex 2.44
指定された回数だけ再帰的に、上下分割して小さな画像を横に並べるやつ。

```scheme
(define (up-split painter n)
  (if (= n 0)
      painter
      (let ((smaller (up-split painter (- n 1))))
           (below painter (beside smaller smaller)))))
```

#### ex 2.45
さっきのup-splitのbesideとbelowを抽象化するだけ。

```scheme
(define (split f1 f2)
  (lambda (painter n)
    (if (= n 0)
        painter
        (let ((smaller ((split f1 f2) painter (- n 1))))
          (f1 painter (f2 smaller smaller))))))

(define up-split (split below beside))
(define right-split (split beside below))
```

#### ex 2.46

```scheme
(define make-vect cons)
(define xcor-vect car)
(define ycor-vect cdr)

(define (add-vect v1 v2)
  (make-vect
   (+ (xcor-vect v1) (xcor-vect v2))
   (+ (ycor-vect v1) (ycor-vect v2))))

(define (sub-vect v1 v2)
  (make-vect
   (- (xcor-vect v1) (xcor-vect v2))
   (- (ycor-vect v1) (ycor-vect v2))))

(define (scale-vect v1 s)
  (make-vect
   (* (xcor-vect v1) s)
   (* (ycor-vect v1) s)))

(add-vect (make-vect 1 2) (make-vect 3 4))
; => (4 . 6)
(sub-vect (make-vect 3 4) (make-vect 1 2))
; => (2 . 2)
(scale-vect (make-vect 3 4) 5)
; => (15 . 20)
```

#### ex 2.47

```scheme
(define (make-frame1 origin edge1 edge2)
  (list origin edge1 edge2))

(define (make-frame2 origin edge1 edge2)
  (cons origin (cons edge1 edge2)))

; どっちの実装でも同じ
(define (origin-frame frame)
  (car frame))

; cdrのcar これもどっちの実装でも同じ
(define (edge1-frame frame)
  (cadr frame))

; cdrのcdrのcar list実装用
(define (edge2-frame1 frame)
  (caddr frame))

; cdrのcdr cons実装用
(define (edge2-frame2 frame)
  (cddr frame))

(define frame1 (make-frame1 (make-vect 1 2) (make-vect 3 4) (make-vect 5 6)))
(define frame2 (make-frame2 (make-vect 1 2) (make-vect 3 4) (make-vect 5 6)))

(origin-frame frame1)
; => (1 . 2)
(edge1-frame frame1)
; => (3 . 4)
(edge2-frame1 frame1)
; => (5 . 6)
(origin-frame frame2)
; => (1 . 2)
(edge1-frame frame2)
; => (3 . 4)
(edge2-frame2 frame2)
; => (5 . 6)
```

#### ex 2.48
このパターンもういいよ・・・

```scheme
(define make-segment cons)
(define start-segment car)
(define end-segment cdr)
```

