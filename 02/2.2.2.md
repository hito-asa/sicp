### 2.2.2 階層構造

リスト内にリストがある階層構造。  
この例だとpair内のpairで二分木ですね。
```scheme
(cons (list 1 2) (list 3 4))
```

count-leaves
```
(define (length items)
  (if (null? items)
      0
      (+ 1 (length (cdr items)))))

(define (count-leaves items)
  (cond ((null? items) 0)
        ((not (pair? items)) 1)
        (else (+ (count-leaves (car items))
                 (count-leaves (cdr items))))))

(define x (cons (list 1 2) (list 3 4)))
(length x)
; => 3
(count-leaves x)
; => 4

(list x x)
; => (((1 2) 3 4) ((1 2) 3 4))
(length (list x x))
; => 2
(count-leaves (list x x))
; => 8
```

#### ex 2.24
![box and pointer diagram](https://raw.github.com/hito-asa/sicp/master/02/ex2.24.png)

#### ex 2.25

```scheme
(define x (list 1 3 (list 5 7) 9))
(car (cdr (car (cdr (cdr x)))))
; => 7

(define x (list (list 7)))
(car (car x))
; => 7

(define x (list 1 (list 2 (list 3 (list 4 (list 5 (list 6 7)))))))
(car (cdr (car (cdr (car (cdr (car (cdr (car (cdr (car (cdr x))))))))))))
; => 7
```

#### ex 2.26

```scheme
(define x (list 1 2 3))
(define y (list 4 5 6))

(append x y)
; => (1 2 3 4 5 6)

(cons x y)
; => ((1 2 3) 4 5 6)

(list x y)
; => ((1 2 3) (4 5 6))
```

#### ex 2.27

普通のreverse  
（こっからracket使い始めてるので、組み込み手続きの再定義ができなくなってます）
```scheme
(define (myreverse l)
  (let iter ((left l) (result (list)))
    (cond ((null? left) result)
          ((pair? left) (iter (cdr left) (cons (car left) result)))
          (else left))))

(myreverse (list (list 1 2) (list 3 4)))
; => ((3 4) (1 2))
```

deep-reverse  
(car left) を (deep-reverse (car lefta)) に置き換える
```scheme
(define (deep-reverse l)
  (let iter ((left l) (result (list)))
    (cond ((null? left) result)
          ((pair? left) (iter (cdr left) (cons (deep-reverse (car left)) result)))
          (else left))))

(deep-reverse (list (list 1 2) (list 3 4)))
; => ((4 3) (2 1))
```

#### ex 2.28

flattenな感じ  
基本はcount-leaves同様の作り
```scheme
(define (fringe l)
    (cond ((null? l) l)
          ((not (pair? l)) (list l))
          (else (append (fringe (car l)) (fringe (cdr l))))))

(fringe (list (list 1 (list 5 6)) (list 3 4)))
; => (1 5 6 3 4)
```

#### ex 2.29

まずは与えられた二進モービルの定義
```scheme
(define (make-mobile left right)
  (list left right))

(define (make-branch length structure)
  (list length structure))
```

選択子  
これはそのまま
```scheme
(define left-branch car)
(define right-branch (lambda (x) (car (cdr x))))
(define branch-length car)
(define branch-structure (lambda (x) (car (cdr x))))
```

total-weight  
二進モービルの解釈あってるかどうか・・・モービル自体はstructureと同義（なはず）
```scheme
(define (total-weight mobile)
  (define (total structure)
    (if (number? structure)
        structure
        (+ (total (branch-structure (left-branch structure)))
           (total (branch-structure (right-branch structure))))))
  (total mobile))

(total-weight (make-mobile (make-branch 1 2)
                           (make-branch 3
                                        (make-mobile (make-branch 4 5)
                                                     (make-branch 6 7)))))
; => 14
```

釣り合っているかどうかを確認する述語の設計。  
以下擬似コード  
モービルを再帰的に処理し、左右の回転力の一致を確かめていく
```scheme
(define (balanced? structure)
  (if (mobile? structure)
      (and (= (left-rotate-power (left-branch structre))
              (right-rotate-power (right-branch structure)))
           (balanced? (branch-structure (left-branch structure)))
           (balanced? (branch-structure (right-branch structure))))
      #t))
```

モービルと枝の構成子がlistからconsに変わったらどうなるか？  
total-weight?やbalanced?はモービルと枝の内部構造には依存せず、選択子にのみ依存しているので、影響は選択子のみ。

#### 木の写像

木に対する再帰を組み合わせたmap
```scheme
(define (scale-tree tree factor)
  (cond ((null? tree) (list))
        ((not (pair? tree)) (* tree factor))
        (else (cons (scale-tree (car tree) factor)
                    (scale-tree (cdr tree) factor)))))

(scale-tree (list 1 (list 2 (list 3 4) 5) (list 6 7)) 10)
; => (10 (20 (30 40) 50) (60 70))
```

組み込みのmapを利用した、木に対するmap
```scheme
(define (scale-tree tree factor)
  (map (lambda (subtree)
         (if (pair? subtree)
             (scale-tree subtree factor)
             (* subtree factor)))
       tree))

(scale-tree (list 1 (list 2 (list 3 4) 5) (list 6 7)) 10)
; => (10 (20 (30 40) 50) (60 70))
```

#### ex 2.30

square-treeの定義  
factorを取らない以外はscale-treeと同じ構造  
自分でconsしていく版とmap版両方書いてみる
```scheme
(define (square-tree tree)
  (map (lambda (subtree)
         (if (pair? subtree)
             (square-tree subtree)
             (* subtree subtree)))
       tree))

(square-tree (list (list 1 2) (list 3 (list 4 5))))
; => ((1 4) (9 (16 25)))

(define (square-tree tree)
  (cond ((null? tree) ())
        ((not (pair? tree)) (* tree tree))
        (else (cons (square-tree (car tree))
                    (square-tree (cdr tree))))))

(square-tree (list (list 1 2) (list 3 (list 4 5))))
; => ((1 4) (9 (16 25)))
```

#### ex 2.31

自分でconsする版をベースにfactor倍してたり二乗してたところを抽象化
```scheme
(define (tree-map proc tree)
  (cond ((null? tree) ())
        ((not (pair? tree)) (proc tree))
        (else (cons (tree-map proc (car tree))
                    (tree-map proc (cdr tree))))))

(tree-map (lambda (x) (* x x)) (list (list 1 2) (list 3 (list 4 5))))
; => ((1 4) (9 (16 25)))
```

#### ex 2.32

受け取ったリストの組合せのリストを返す  
```scheme
(define (subsets s)
  (if (null? s)
      (list (list))
      (let ((rest (subsets (cdr s))))
        (append rest (map (lambda (x) (cons (car s) x)) rest)))))

(subsets (list 1 2 3))
; => (() (3) (2) (2 3) (1) (1 3) (1 2) (1 2 3))
```

部分リストの組合せのリストに、1つづつ値を全部に付け足してく感じでうまく行く。
```
nil
nil (1)
nil (1) (2) (1 2)
nil (1) (2) (1 2) (3) (1 3) (2 3) (1 2 3)
```
みたいな。

