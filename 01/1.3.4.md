
### 1.3.4 値として返される手続き

手続きを値として返す。  
前にlamdaとしてちょっと出てきた。

```scheme
(define (square a) (* a a))
((lambda (x y z) (+ x y (square z))) 1 2 3)
; => 12
```

平均緩和法を適用した手続きを返すaverage-dampを定義

```scheme
(define (average a b) (/ (+ a b) 2.0))
(define (average-damp f)
  (lambda (x) (average x (f x))))

(define (square a) (* a a))
((average-damp square) 10)
; => 55.0
```

平方根の手続きをaverage-dampを利用して書き換える。

```scheme
;元の記述
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))

(define (average a b) (/ (+ a b) 2.0))

(define (sqrt x)
  (fixed-point (lambda (y) (average y (/ x y))) 1.0))

;average-dampを利用
(define (average-damp f)
  (lambda (x) (average x (f x))))

(define (sqrt x)
  (fixed-point (average-damp (lambda (y) (/ x y))) 1.0))
```

この抽象化を立方根を求める手続きに適用する。

```scheme
(define (cube-root x)
  (fixed-point (average-damp (lambda (y) (/ x (square y)))) 1.0))
```

#### Newron法

微分する手続き  
微分された手続きを返す。

```scheme
(define dx 0.00001)

(define (deriv g)
  (lambda (x)
    (/ (- (g (+ x dx)) (g x)) dx)))

(define (cube x) (* x x x))

((deriv cube) 5)
; => 75.00014999664018
```

Newton法をfixed-pointを利用して定義

```scheme
(define (newton-transform g)
  (lambda (x)
    (- x (/ (g x) ((deriv g) x)))))

(define (newtons-method g guess)
  (fixed-point (newton-transform g) guess))
```

Newton法を利用した平方根を求める手続き。  
（<code>y -> y<sup>2</sup> - x</code> の零点を求める）

```scheme
(define (sqrt x)
  (newtons-method (lambda (y) (- (square y) x)) 1.0))

(sqrt 25)
; => 5.0
```

#### 抽象と第一級手続き

関数とその変換を受け取り、不動点を計算する手続きを定義する。

```scheme
(define (fixed-point-of-transform g transform guess)
  (fixed-point (transform g) guess))
```

これを利用して、平均緩和法とNewton法を利用した平方根を求める手続きを定義する

```scheme
;平均緩和法
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (/ x y))
                            average-damp
                            1.0))

;Newton法
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (- (square y) x))
                            newton-transform
                            1.0))
```

#### ex 1.40
Newton法を定義
```scheme
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))

(define dx 0.00001)

(define (deriv g)
  (lambda (x)
    (/ (- (g (+ x dx)) (g x)) dx)))

(define (newton-transform g)
  (lambda (x)
    (- x (/ (g x) ((deriv g) x)))))

(define (newtons-method g guess)
  (fixed-point (newton-transform g) guess))
```

以下の式で <code>x<sup>3</sup> + ax<sup>2></sup> + bx + c</code> の零点を近似する
```
(newtons-method (cubic a b c) 1)
```

必要となるcubicの定義は
```scheme
(define (cubic a b c)
  (lambda (x) (+ (* x x x) (* a (* x x)) (* b x) c)))
```

多分合ってる
```scheme
(newtons-method (cubic 0 0 -8) 1)
; => 2.000000000036784

(newtons-method (cubic -1 -1 -2) 1)
; => 2.000000000000005
```

#### ex 1.41
1つの引数を取る手続きを取り、受け取った手続きを2回作用させる手続を返す手続き
```scheme
(define (double g)
  (lambda (x) (g (g x))))

(define (inc x) (+ x 1))

((double inc) 5)
; => 6

(((double double) inc) 5)
; => 9

(((double (double double)) inc) 5)
; => 21
```

こんな感じで16回適用されることになる。
```scheme
(let (
       (f (lambda (x) (double (double x))))
     )
     (((double f) inc) 5))

(let (
       (ff (lambda (x) (double (double (double (double x))))))
     )
     ((ff inc) 5))
```

#### ex 1.42
<code>x -> f(g(x))</code>  
関数の合成、そのまま書く
```scheme
(define (compose f g)
  (lambda (x) (f (g x))))

(define (square x) (* x x))
(define (inc x) (+ x 1))
((compose square inc) 6)
; => 49
```

#### ex 1.43
関数fをn回作用する手続を定義
```scheme
(define (compose f g)
  (lambda (x) (f (g x))))

(define (repeated f n)
  (define (iter g i)
          (if (< i n)
              (iter (compose f g) (+ i 1))
              g))
  (iter f 1))

(define (square x) (* x x))
((repeated square 2) 5)
```

#### ex 1.44
関数の平滑化  
平滑化関数
```scheme
(define dx 0.01)

(define (smooth f)
  (let ((prev (lambda (x) (f (- x dx))))
        (mid (lambda (x) (f x)))
        (next (lambda (x) (f (+ x dx)))))
  (lambda (x) (/ (+ (prev x) (mid x) (next x)) 3.0))))

;合っている気がする
((smooth (lambda (x) (* x x))) 3)
; => 9.000066666666667
```

n重平滑化関数
```scheme
(define (compose f g)
  (lambda (x) (f (g x))))

(define (repeated f n)
  (define (iter g i)
          (if (< i n)
              (iter (compose f g) (+ i 1))
              g))
  (iter f 1))

(define (n-fold-smooth f n)
  ((repeated smooth n) f))

;たぶん合っている
((n-fold-smooth (lambda (x) (* x x)) 1) 3)
; => 9.000066666666667
((n-fold-smooth (lambda (x) (* x x)) 4) 3)
; => 9.000266666666667
```

#### ex 1.45
四乗根を求めるには<code>y -> x/y<sup>3</sup></code>を2回平方緩和すると良いらしい。

```scheme
(define (compose f g)
  (lambda (x) (f (g x))))

(define (repeated f n)
  (define (iter g i)
          (if (< i n)
              (iter (compose f g) (+ i 1))
              g))
  (iter f 1))

(define (power x n)
  (if (> n 0)
      (* x (power x (- n 1)))
      1))

(define (average a b) (/ (+ a b) 2.0))
(define (average-damp f)
  (lambda (x) (average x (f x))))

(define tolerance 0.00001)

;無限ループしないように100万回で諦める
(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess count)
    (let ((next (f guess)))
      (cond ((> count 1000000) 0)
            ((close-enough? guess next) next)
            (else (try next (+ count 1))))))
  (try first-guess 1))


;2乗根
(define (rt2 x)
  (fixed-point (average-damp (lambda (y) (/ x y))) 1.0))

;3乗根
(define (rt3 x)
  (fixed-point (average-damp (lambda (y) (/ x (* y y)))) 1.0))

;4乗根
(define (rt4 x)
  (fixed-point ((repeated average-damp 2) (lambda (y) (/ x (power y 3)))) 1.0))

(rt4 81)
; => 3.000000000000033
```

ここまではこれまでどおりうまく行く。  
n乗根を定義。とりあえず平方緩和は2回のまま。

```scheme
(define (rt x n)
  (fixed-point ((repeated average-damp 2) (lambda (y) (/ x (power y (- n 1))))) 1.0))

;xのn乗のn乗根を出す
;100万回の計算で収束しないところのnを返す
(define (rt-powerof x n)
  (let ((current (rt (power x n) n)))
    (cond ((= current 0) n)
          ((= n 100) n)
          (else (rt-powerof x (+ n 1))))))
```

試してみると、8乗根は収束しないらしい。  
入れた数値は適当。

```scheme
(rt-powerof 2 1)
; => 8
(rt-powerof 3 1)
; => 8
(rt-powerof 5 1)
; => 8
(rt-powerof 7331 1)
; => 8
(rt-powerof 3.14 1)
; => 8
```

平方緩和の回数を3にして試してみる。  
15乗根までいけるようになる。

```scheme
(define (rt x n)
  (fixed-point ((repeated average-damp 3) (lambda (y) (/ x (power y (- n 1))))) 1.0))

(rt-powerof 2 1)
; => 16
(rt-powerof 3 1)
; => 16
(rt-powerof 5 1)
; => 16
(rt-powerof 7331 1)
; => 16
(rt-powerof 3.14 1)
; => 16
```


平方緩和の回数を4にして試してみる。  
31乗根までいけるようになる。

```scheme
(define (rt x n)
  (fixed-point ((repeated average-damp 4) (lambda (y) (/ x (power y (- n 1))))) 1.0))

(rt-powerof 2 1)
; => 32
(rt-powerof 3 1)
; => 32
(rt-powerof 5 1)
; => 32
(rt-powerof 7331 1)
; => 32
(rt-powerof 3.14 1)
; => 32
```

というわけで平方緩和の回数をnの平方根にして試してみる。  
2,3,5,3.14については100まで達成したのでよさそうだけど、7331は80乗が収束しない。  
計算回数を100万から5000万に変えてもダメ。  
平方緩和の回数を1増やしても2倍にしてもダメ。  

```scheme
(define (rt x n)
  (fixed-point ((repeated average-damp (rt2 n)) (lambda (y) (/ x (power y (- n 1))))) 1.0))

(rt-powerof 2 1)
; => 100
(rt-powerof 3 1)
; => 100
(rt-powerof 5 1)
; => 100
(rt-powerof 7331 1)
; => 80
(rt-powerof 3.14 1)
; => 100
```

計算精度対策でclose-enough?を問題1.7でやった変化率をとる処理に書き換える。
が、だめ。

```scheme
(define tolerance 0.001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (/ (- v1 v2) v1)) tolerance))
  (define (try guess count)
    (let ((next (f guess)))
      (cond ((> count 1000000) 0)
            ((close-enough? guess next) next)
            (else (try next (+ count 1))))))
  (try first-guess 1))

(rt-powerof 7331 1)
; => 80
```

入力値がある程度以上大きいとダメっぽい。もう諦めた。

#### ex 1.46

こんな感じな気がする（動きません）。

```scheme
(define (iterative-improve improve good-enough?)
  (lambda (first-guess)
    ((let ((try (lambda (guess)
      (let ((next (improve guess)))
        (if (good-enough? guess next)
            next
            (try next))))))))
     (try first-guess 1)))

(define (fixed-point f first-guess)
  ((iterative-improve f (lambda (v1 v2) (< (abs (/ (- v1 v2) v1)) 0.001))) 
   1.0))

(define (sqrt x)
  ((iterative-improve (lambda (y) (/ x y)) average-damp) 1.0))

```

