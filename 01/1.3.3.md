
### 1.3.3 一般的方法としての手続き

```scheme
(define (average a b) (/ (+ a b) 2.0))

(define (close-enough? x y) (< (abs (- x y)) 0.001))

(define (search f neg-point pos-point)
  (let ((midpoint (average neg-point pos-point)))
    (if (close-enough? neg-point pos-point)
        midpoint
        (let ((test-value (f midpoint)))
          (cond ((positive? test-value)
                 (search f neg-point midpoint))
                ((negative? test-value)
                 (search f midpoint pos-point))
                (else midpoint))))))

(define (him f a b)
  (let ((a-value (f a))
        (b-value (f b)))
    (cond ((and (negative? a-value) (positive? b-value))
           (search f a b))
          ((and (negative? b-value) (positive? a-value))
           (search f b a))
          (else (error "Values are not of opposite sign" a b)))))

(him sin 2.0 4.0)
; => 3.14111328125

(him (lambda (x) (- (* x x x) (* 2 x) 3)) 1.0 2.0)
; => 1.89306640625
```

#### 関数の不動点の探索

```scheme
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))

(fixed-point cos 1.0)
; => 0.7390822985224024

(fixed-point (lambda (y) (+ (sin y) (cos y))) 1.0)
; => 1.2587315962971173

(define (sqrt x)
  (fixed-point (lambda (y) (/ x y)) 1.0))
(sqrt 2)
; => 収束しない（無限ループ）

(define (sqrt x)
  (define (average a b) (/ (+ a b) 2.0))
  (fixed-point (lambda (y) (average y (/ x y))) 1.0))
(sqrt 2)
; => 1.4142135623746899
```

#### ex 1.35
φ<sup>2</sup> = φ + 1  
（両辺をφで割って）  
φ = 1 + 1/φ

```scheme
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))

(fixed-point (lambda (y) (+ 1 (/ 1 y))) 1.0)
; => 1.6180327868852458
```

#### ex 1.36
x<sup>x</sup> = 1000  
log(x<sup>x</sup>) = log(1000)  
x * log(x) = log(1000)  
x = log(1000) / log(x)

```scheme
(define tolerance 0.00001)

(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (display next)
      (newline)
      (if (close-enough? guess next)
        next
        (try next))))
  (try first-guess))

(fixed-point (lambda (y) (+ 1 (/ 1 y))) 1.0)

(fixed-point (lambda (y) (/ (log 1000) (log y))) 2.0)
; => 4.555532270803653
```

#### ex 1.37
















