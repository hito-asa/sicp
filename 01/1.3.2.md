
### 1.3.2 lambdaを使う手続きの構築

lambdaによる手続きの構築
```scheme
(lambda (x) (+ x 4))

(lambda (x) (/ 1.0 (* x (+ x 2))))
```

lambdaを使ったpi-sum, integralの定義
```scheme
(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a) (sum term (next a) next b))))

(define (pi-sum a b)
  (sum (lambda (x) (/ 1.0 (* x (+ x 2))))
       a
       (lambda (x) (+ x 4))
       b))

(define (integral f a b dx)
  (* (sum f
          (+ a (/ dx 2.0))
          (lambda (x) (+ x dx))
          b)
     dx))
```

lambdaと名前の対応付け
```scheme
(define (plus4 x) (+ x 4))

(define plus4 (lambda (x) (+ x 4)))
```

lambdaの返り値への適用
```scheme
((lambda (x y z) (+ x y (square z))) 1 2 3)
; => 12
```

letによる局所的変数の作成
```scheme
(define (square x) (* x x))

(define (f x y)
  (define (fh a b)
    (+ (* x (square a))
       (* y b)
       (* a b)))  
  (fh (+ 1 (* x y))
      (- 1 y)))

; 普通こっち思い浮かぶよね（※１）
(define (f x y)
  (define a (+ 1 (* x y)))
  (define b (- 1 y))
  (+ (* x (square a))
     (* y b)
     (* a b)))  

; 局所変数a,bの束縛に名無しの手続きlambdaを用いる
(define (f x y)
  ((lambda (a b)
    (+ (* x (square a))
       (* y b)
       (* a b)))
   (+ 1 (* x y))
   (- 1 y)))

; シンタックスシュガーとしてletを用いる
; ※１のdefineの局所化っぽくなる
(define (f x y)
  (let ((a (+ 1 (* x y)))
        (b (- 1 y)))
       (+ (* x (square a))
         (* y b)
         (* a b)))

```

letの局所性
```scheme
(define x 5)

(+ (let ((x 3))
     (+ x (* x 10)))
   x)
; => 38
```

letの局所変数の依存関係
```scheme
(define x 2)

(let ((x 3)
      (y (+ x 2)))
  (* x y))
; => 12
```

#### ex 1.34
```scheme
(define (f g) (g 2))

(define (square x) (* x x))

(f square)
; => 4

(f (lambda (z) (* z (+ z 1))))
; => 6

(f f)
; => (f 2)
;   => (2 2) 適用できない
```

